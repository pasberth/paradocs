%include ../lib/stdhtml.pdoc

%rule block cheatsheet
  %before %""<table class="cheatsheet">""
  %after %"</table>"
  %def | %tr%left

%rule block cheatsheet tr
  %before %"<tr>"
  %after %"</tr>"

%rule block cheatsheet tr left
  %extend inline
  %extend block
  %before %""<td class="left">""
  %after %""</td>""
  %def | %right

%rule block cheatsheet tr right
  %extend inline
  %extend block
  %before %""<td class="right">""
  %after %""</td>""
  %def | %tr %left

%html
%head
%mktoc
:simple.css:
%style
"""
.cheatsheet {
  width: 100%;
}
.left, .right {
  width: 50%;
  border-style: none;
}
"""
%body

= Paradocs cheatsheet
%toc

== Formatted Text

%cheatsheet
|+"*bold*"+|*bold*
|+"_italic_"+|_italic_
|+"+monospace+"+|+monospace+
|+"super^script^"+|super^script^
|+"sub~script~"+|sub~script~
|+"*_bold italic_*"+|*_bold italic_*

== Links

%cheatsheet
|+"`cheatsheet.html`here`"+|`cheatsheet.html`here`

== Read & Include files

%code-frame
%title `cheatsheet/hello.pdoc`cheatsheet/hello.pdoc`
%content
%read cheatsheet/hello.pdoc

%cheatsheet
|%pre"%read cheatsheet/hello.pdoc"|%read cheatsheet/hello.pdoc
|%pre"%include cheatsheet/hello.pdoc"|%include cheatsheet/hello.pdoc

== Lists

%cheatsheet
|%pre"""
%ul
- item 1
- item 2
- item 3
"""|%ul
- item 1
- item 2
- item 3


== Tables

%cheatsheet
|%pre"""
%table
=||column A|column B|column C
.|row 1|cell A1|cell B1|cell C1
.|row 2|cell A2|cell B2|cell C2
.|row 3|cell A3|cell B3|cell C3
"""|%table
=||column A|column B|column C
.|row 1|cell A1|cell B1|cell C1
.|row 2|cell A2|cell B2|cell C2
.|row 3|cell A3|cell B3|cell C3

== Quoting Text

=== Quoting macro characters

%cheatsheet
|%pre"""
\*hello\*
"""|\*hello\*
|%pre"""
"*hello*"
"""|"*hello*"
|%pre"""
""
*hello*
""
"""|""
*hello*
""

=== Quoting Linebreaks

%cheatsheet
|%pre"""
first line. \
next line.
"""|first line. \
next line.
|%pre"""
""
first line.
next line.
""
"""|""
first line.
next line.
""

=== Quoting "`\\'"

%cheatsheet
|%pre"""
\\
"""|\\
|%pre"""
"\\"
"""|"\\"

=== Quoting "`\"'"

%cheatsheet
|%pre"""
\"
"""|\"
|%pre"""
"\""
"""|"\""

== Raw HTML

%cheatsheet
|%pre"""
%"<strong>hello</strong>"
"""|%"<strong>hello</strong>"
|%pre"""
%""
<strong>hello</strong>
""
"""|%""
<strong>hello</strong>
""
