%include ../lib/stdhtml.paradoc

%rule block cheatsheet
  %render-before <table class=\"cheatsheet\">
  %render-after </table>
  %def | %tr%left

%rule block cheatsheet tr
  %render-before <tr>
  %render-after </tr>

%rule block cheatsheet tr left
  %extend everywhere
  %extend inline
  %extend block
  %render-before <td class=\"left\">
  %render-after </td>
  %def | %right

%rule block cheatsheet tr right
  %extend everywhere
  %extend inline
  %extend block
  %render-before <td class=\"right\">
  %render-after </td>
  %def | %tr%left

= Paradocs cheatsheet

== Formatted Text

%cheatsheet
|%pre"*bold*"|*bold*
|%pre"_italic_"|_italic_
|%pre"+monospace+"|+monospace+
|%pre"super^script^"|super^script^
|%pre"sub~script~"|sub~script~
|%pre"*_bold italic_*"|*_bold italic_*

== Links

%cheatsheet
|%pre"`cheatsheet.html`here`"|`cheatsheet.html`here`

== Lists

%cheatsheet
|%pre"""
%ul
- item 1
- item 2
-- item 2.1
--- item 2.1.1
--- item 2.1.2
-- item 2.2
- item 3
- item 4
"""
|%ul
- item 1
- item 2
-- item 2.1
--- item 2.1.1
--- item 2.1.2
-- item 2.2
- item 3
- item 4


== Tables

%cheatsheet
|%pre"""
%table
=||column A|column B|column C
.|row 1|cell A1|cell B1|cell C1
.|row 2|cell A2|cell B2|cell C2
.|row 3|cell A3|cell B3|cell C3
"""
|%table
=||column A|column B|column C
.|row 1|cell A1|cell B1|cell C1
.|row 2|cell A2|cell B2|cell C2
.|row 3|cell A3|cell B3|cell C3

== Raw HTML

%cheatsheet
|%pre"""
%raw
<strong>hello</strong>
"""
|%raw
<strong>hello</strong>
|%pre"""
%raw"<strong>hello</strong>"
"""
|%raw"<strong>hello</strong>"
|%pre""""
%raw"""
<strong>hello</strong>
"""
""""
|%raw"""
<strong>hello</strong>
"""
